plugins {
    id 'fabric-loom' version '1.8-SNAPSHOT'
    id 'maven-publish'
}

version = mod_version
group = mod_group

base {
    archivesName = "${mod_id}-fabric-${minecraft_version}"
}

repositories {
    mavenLocal()

    maven { url = "https://maven.parchmentmc.org" } // Parchment
    maven { url "https://maven.shedaniel.me/" }

    // Appleskin
    repositories {
        maven { url "https://maven.ryanliptak.com/" }
    }

    // Cloth Config
    maven {
        name = "CurseMaven"
        url = "https://cursemaven.com"
        content {
            includeGroup "curse.maven"
        }
    }

    // Farmer's Delight Refabricated
    maven { url = "https://maven.greenhouse.lgbt/releases"}

    // JEI
    maven {
        name = "BlameJared"
        url = "https://maven.blamejared.com/"
    }

    // Porting Lib
    maven { url "https://mvn.devos.one/releases/" }
    maven {
        url "https://maven.jamieswhiteshirt.com/libs-release"
        content {
            includeGroup "com.jamieswhiteshirt"
        }
    }
    maven {
        url "https://jitpack.io/"
        content {
            excludeGroup "io.github.fabricators_of_create"
        }
    }
}

fabricApi {
    configureDataGeneration()
}

dependencies {
    // To change the versions see the gradle.properties file
    minecraft "com.mojang:minecraft:${minecraft_version}"

    // Parchment
    mappings loom.layered() {
        officialMojangMappings()
        parchment("org.parchmentmc.data:parchment-${minecraft_version}:${parchment_version}")
    }

    // Fabric
    modImplementation "net.fabricmc:fabric-loader:${fabric_loader_version}"
    modImplementation "net.fabricmc.fabric-api:fabric-api:${fabric_api_version}+${minecraft_version}"

    // Farmer's Delight
    modImplementation "vectorwing:FarmersDelight:${minecraft_version}-${farmersdelight_version}+refabricated"

    // Porting Lib
    for (String module in port_lib_modules.split(", ")) {
        modApi("io.github.fabricators_of_create.Porting-Lib:${module}:${port_lib_version}+${minecraft_version}") {
            exclude(group: "net.fabricmc")
        }
    }

    // Test mods
    //modImplementation "curse.maven:modmenu-308702:5162837" // Mod Menu, required by JEI
    //modLocalRuntime "mezz.jei:jei-${minecraft_version}-fabric:${jei_version}"
    //modLocalRuntime "squeek.appleskin:appleskin-fabric:${appleskin_version}"
}

processResources {
    inputs.property "version", project.version

    filesMatching("fabric.mod.json") {
        expand "version": project.version
    }
}

tasks.withType(JavaCompile).configureEach {
    it.options.release = 21
}

java {
    // Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
    // if it is present.
    // If you remove this line, sources will not be generated.
    withSourcesJar()

    sourceCompatibility = JavaVersion.VERSION_21
    targetCompatibility = JavaVersion.VERSION_21
}

jar {
    from("LICENSE") {
        rename { "${it}_${project.base.archivesName.get()}"}
    }
}

// configure the maven publication
publishing {
    publications {
        create("mavenJava", MavenPublication) {
            artifactId = mod_id
            from components.java
        }
    }

    // See https://docs.gradle.org/current/userguide/publishing_maven.html for information on how to set up publishing.
    repositories {
        // Add repositories to publish to here.
        // Notice: This block does NOT have the same function as the block in the top level.
        // The repositories here will be used for publishing your artifact, not for
        // retrieving dependencies.
    }
}