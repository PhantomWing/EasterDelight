plugins {
    id 'fabric-loom' version '1.8-SNAPSHOT'
    id 'maven-publish'
}

version = project.mod_version
group = project.maven_group

base {
    archivesName = "${project.archives_base_name}-fabric-${project.minecraft_version}"
}

repositories {
    maven { url = "https://maven.parchmentmc.org" } // Parchment

    maven { url "https://maven.shedaniel.me/" }
    maven {
        url "https://maven.ryanliptak.com/"
    }

    maven {
        name = "CurseMaven"
        url = "https://cursemaven.com"
        content {
            includeGroup "curse.maven"
        }
    }

    maven {
        name = "Greenhouse Maven"
        url = 'https://repo.greenhouse.house/releases/'
    }
    maven {
        name = "Greenhouse Maven"
        url = 'https://repo.greenhouse.house/snapshots/' // Porting Lib Hotfixes
    }
    maven { url "https://mvn.devos.one/snapshots/" } // Porting Lib Betas
    maven {
        url "https://jitpack.io/" // Fabric ASM
        content {
            excludeGroup "io.github.fabricators_of_create"
        }
    }
}

fabricApi {
    configureDataGeneration()
}

dependencies {
    // To change the versions see the gradle.properties file
    minecraft "com.mojang:minecraft:${project.minecraft_version}"

    // Parchment
    mappings loom.layered() {
        it.officialMojangMappings {
            setNameSyntheticMembers(false)
        }
        it.parchment("org.parchmentmc.data:parchment-${project.parchment_version}")
    }

    // Fabric
    modImplementation "net.fabricmc:fabric-loader:${project.fabric_loader_version}"
    modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_api_version}"

    // Dependencies
    modImplementation("vectorwing:FarmersDelight:${project.farmers_delight_version}") {
        exclude(group: "net.fabricmc")
        // exclude(group: "io.github.fabricators-of-create")
        // If you want to exclude Porting Lib to instead provide your own version of Porting Lib, uncomment the above line..
    }

    // Test mods
    modImplementation "curse.maven:cloth-config-348521:5729125"
    modLocalRuntime("curse.maven:jei-238222:5802635")
    modLocalRuntime("squeek.appleskin:appleskin-fabric:${appleskin_version}")
}

processResources {
    inputs.property "version", project.version

    filesMatching("fabric.mod.json") {
        expand "version": project.version
    }
}

tasks.withType(JavaCompile).configureEach {
    it.options.release = 21
}

java {
    // Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
    // if it is present.
    // If you remove this line, sources will not be generated.
    withSourcesJar()

    sourceCompatibility = JavaVersion.VERSION_21
    targetCompatibility = JavaVersion.VERSION_21
}

jar {
    from("LICENSE") {
        rename { "${it}_${project.base.archivesName.get()}"}
    }
}

// configure the maven publication
publishing {
    publications {
        create("mavenJava", MavenPublication) {
            artifactId = project.archives_base_name
            from components.java
        }
    }

    // See https://docs.gradle.org/current/userguide/publishing_maven.html for information on how to set up publishing.
    repositories {
        // Add repositories to publish to here.
        // Notice: This block does NOT have the same function as the block in the top level.
        // The repositories here will be used for publishing your artifact, not for
        // retrieving dependencies.
    }
}